// Endpoint for creating a Stripe payment link
.arguments
   price:string
   quantity:int
   success_url:string
.description:Creates a new Stripe payment link for the specified price and quantity, and returns the link to the caller

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/price
validators.mandatory:x:@.arguments/*/success_url
validators.default:x:@.arguments
   quantity:int:1

// Ensuring user it authorised to invoke endpoint.
auth.ticket.verify:admin,root

// Retrieving Stripe token from configuration.
.token
set-value:x:@.token
   strings.concat
      .:"Bearer "
      config.get:"magic:stripe:token"

// Invoking Stripe to create payment link here.
http.post:"https://api.stripe.com/v1/payment_links"
   convert:true
   headers
      Authorization:x:@.token
      Content-Type:application/x-www-form-urlencoded
   payload
      line_items[0][price]:x:@.arguments/*/price
      line_items[0][quantity]:x:@.arguments/*/quantity
      after_completion[type]:redirect
      after_completion[redirect][url]:x:@.arguments/*/success_url

// Forward evaluating [return] invocation.
unwrap:x:./*/return/*

// Returning payment link to caller.
return
   purchase_link:x:@http.post/*/content/*/url